/*
 * YaST2: Local security configuration
 *
 * Description:
 *   User interface functions.
 *
 * Authors:
 *   Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

/*
 * Usage:
 *   include "security_ui.ycp";
 *   map BUTTONS = CallFunction(`buttons());
 *   map STRINGS = CallFunction(`your_helps());
 *   map WIDGETS = CallFunction(`your_widgets());
 */

{

include "general.ycp";
include "general_ui.ycp";

/* Menuentry for the YaST2 menu */

define YaST2MenuEntry(map m) ``{
  string mpath = lookup(m,"path");
  string mmenu = lookup(m,"menu");
  string mhelp = lookup(m,"help");
  return [ mpath, $[
    `menuentry    : mmenu,
    `arguments    : [ ],
    `widget       : `RichText(mhelp),
    `codefragment : nil
  ]];
};

/* map2widget -- see security_maps.ycp */

define map2widget(string ID /*, map m*/) ``{
  /* string ID = lookup(m,"ID"); */
  map m = lookup(WIDGETS,ID);
  string label = lookup(m,"Label");
  string value = lookup(m,"Value");
  string widget = lookup(m,"Widget");

  /* "Widget" == "CheckBox" */

  if(widget == "CheckBox") {
    boolean enabled = false;
    if(value == "yes")
      enabled = true;
    return `VBox(
      //VSeparator(),
      `Left(`CheckBox(`id(ID),label,enabled)),
      VSeparator()
    );
  }

  /* "Widget" == "TextEntry" */

  if(widget == "TextEntry") {
    return `VBox(
      `Left(`TextEntry(`id(ID),label,value)),
      //VSeparator(),
      VSeparator()
    );
  }

  /* "Widget" == "IntField" */

  if(widget == "IntField") {
    return `VBox(
      //VSeparator(),
      `Left(`IntField(`id(ID),label,0,65000,tointeger(value))),
      VSeparator()
    );
  }

  /* "Widget" == "???" */

  if(widget != "ComboBox") {
    y2error("Unknown widget: %1", widget);
    return VSeparator();
  }

  /* "Widget" == "ComboBox" */

  list li = lookup(m,"Options");
  list combo = [];
  integer i = 0;

  while(i < size(li)) {
    string|list it = select(li,i);
    string id_t = "";
    string id_s = "";
    if(is(it,string)) {
      id_t = it;
      id_s = it;
    }
    else {
      id_t = select(it,0);
      id_s = select(it,1);
    }
    if(value == id_t)
      combo = add(combo,`item(`id(id_t),id_s,true));
    else
      combo = add(combo,`item(`id(id_t),id_s));
    i=i+1;
  }

  return `VBox(
    `Left(`ComboBox(`id(ID),label,combo)),
    VSeparator()
  );
};

/* Query the widget with `id(ID) for its `Value */

define widget2value(string ID) ``{
  any ret = UI(`QueryWidget(`id(ID),`Value));
  if(is(ret,boolean))
    if(ret==true)
      ret = "yes";
    else
      ret = "no";
  if(is(ret,integer))
    ret = sformat("%1",ret);

  return ret;
};

/* Process the UI input and return `ok or nil */

define processInput() ``{
  any ret = nil;

  while(true) {
    ret = UI(`UserInput());
    if(ret != `abort)
      break;
    any re = Popup(UI(_("Do you really want to abort the security configuration?")));
    if(re==`ok)
      break;
  }

  if(ret == `ok || ret == `finish)
    return `ok;
  if(ret == `cancel || ret == nil)
    return nil;

  return ret;
};

/* Vertical separator */

define VSeparator() ``{
  return `VSpacing(`opt(`vstretch),0.1);
};

/* Horizontal separator */

define HSeparator() ``{
  return `HSpacing(`opt(`hstretch),0.1);
};

/* str2term */

define str2term(symbol B) ``{
  string|void button = lookup(BUTTONS,B);
  if(button==nil)
    return nil;

  term|void Button = nil;

  if(B==`ok || B==`finish || B==`next)
    Button = `ReplacePoint(`id(`rp),`PushButton(`id(B), `opt(`default), button));
  else
    Button = `PushButton(`id(B), button);

  return Button;

  term AbortButton = `PushButton(`id(`back), _("&Abort"));
  term BackButton = `PushButton(`id(`back), _("&Back"));
  term OKButton = `ReplacePoint(`id(`rp),`PushButton(`id(`ok), `opt(`default), _("&OK")));
  term FinishButton = `ReplacePoint(`id(`rp),`PushButton(`id(`finish), `opt(`default), _("&Finish")));
  term NextButton = `ReplacePoint(`id(`rp),`PushButton(`id(`next), `opt(`default), _("&Next")));
  term CancelButton = `PushButton(`id(`cancel), _("&Cancel"));

  if(B=="B") return BackButton;
  if(B=="O") return OKButton;
  if(B=="F") return FinishButton;
  if(B=="A") return AbortButton;
  if(B=="C") return CancelButton;
  return OKButton;
}

/* Bottom buttons -- [Back] and [OK] */

define BottomButtons(list buttons) ``{
  symbol|void B1 = nil;
  symbol|void B2 = nil;
  symbol|void B3 = nil;

  if(buttons == [] ) buttons = [ `back, `abort, `next];

  term TT = `HBox();

  TT = foreach(`s,buttons,``{
    any x = str2term(s);
    TT = add(TT,x);
    any x = `HStretch();
    TT = add(TT, x);
    return TT;
  });

  return remove(TT,size(TT));
};

/* The main dialog widget */

define SuSEDialog(string ID, list buttons, term T) ``{
  //map dmap = lookup(STRINGS(),ID);
  map dmap = lookup(STRINGS,ID);
  string help_text = lookup(dmap,"help");
  string caption = lookup(dmap,"caption");

  UI(`OpenDialog( `opt(`defaultsize),
    `VBox(`Image(`suseheader, "SuSE"), `HBox(
      `HWeight(30, `RichText(`id(`help), help_text)),
      `HWeight(70,
	`HVCenter( `VBox(
	  `Top(`Left(`Heading(`id(`title), caption))),
          VSeparator(),
          `HVCenter(`HVSquash(`HBox(`HSpacing(5),`VBox(`VSpacing(2),T,`VSpacing(2)),`HSpacing(5)))),
          //`HVCenter(`Frame(caption,T)),
          VSeparator(),
          BottomButtons(buttons)
        )))
  ))));
};

/* Update one value in the WIDGETS map from the widget value */

define updateWidget(string ID) ``{
  any val = widget2value(ID);
  WIDGETS = updateValue(WIDGETS,ID,val);
};


/* One security item */

define SecurityItem(any ID, string str) ``{
  term T=`VWeight( 15,
    `PushButton ( `id( ID ), `opt( `hstretch ), str )//)//)
  );
  return T;
};

/* Search the whole settings map for the given string id */

define FindSettings(list settings, string ident) ``{
  map|void ret = nil;

  map|void mm = find(`item,settings,``{
    string key = lookup(item,"name");
    if(key==ident) return true;
    return false;
  });

  if(mm==nil)
    ret = nil;
  else
    ret = lookup(mm,"settings");

  return ret;
};

/* Search the whole settings map for the given map */

define CurrentSettings(list settings, map current) ``{
  string|void ret = nil;

  y2debug("current: %1",current);
  map|void mm = find(`item,settings,``{
    string key = lookup(item,"name");
    if(key=="Default") return false;

    map val = lookup(item,"settings");

    y2debug("key: %1",key);
    y2debug("val: %1",val);
    if(current==val) {
      y2debug("Found");
      return true;
    }
    return false;
  });

  //y2debug("mm: %1",mm);
  if(mm==nil)
    ret = nil;
  else
    ret = lookup(mm,"name");

  //return "Secure";
  //return nil;
  return ret;
};

/* Build a RadioButton group from security_settings */

define SecuritySettings(list settings, map defs) ``{
  string|void current = CurrentSettings(settings,defs);
  //current="Server";

  boolean enab = false;
  if(current==nil) enab=true;

  term T = `VBox();
  T = add(T,`VSpacing(0.3));

  T = foreach(`item,settings,``{
    string key = lookup(item,"name");
    if(key=="Default") return T;

    map val = lookup(item,"settings");
    y2debug("key: %1",key);

    term R = `RadioButton();
    R = add(R,`id(key));
    R = add(R,`opt(`notify));
    R = add(R,key);
    if(key == current) R = add(R,true);
    T = add(T, `Left(R));
    return T;
  });

  T = add(T,`VSpacing(0.5));
  T = add(T,`Left(`RadioButton(`id(`custom),`opt(`notify),"&Custom settings",enab)));
    /* T = add(T,`Left(`HBox(`RadioButton(`id(`custom),"&Custom settings",enab),`Right(`PushButton(`id(`details),"&Change..."))))); */
  T = add(T,`VSpacing(0.3));
  y2debug("T: %1",T);
  return T;
};

define updateWIDGETS(any curr) ``{
  any curr2 = UI(`QueryWidget(`id(`rb),`CurrentButton));
  if(curr2!=curr)
  changed = true;
  string current_defs = UI(`QueryWidget(`id(`rb),`CurrentButton));
  map current_sets = FindSettings(settings,current_defs);
  WIDGETS = defs2options(WIDGETS,current_sets);
}

/* Main security settings dialog */

define SecurityMainDialog(map defs) ``{

  list settings = CallFunction(`security_settings());

  SuSEDialog("Main",[`back,`finish], 
    `HVCenter(
    `VBox(
    `HVSquash(
    `Frame( "Current security settings:",
      `RadioButtonGroup(`id(`rb), SecuritySettings(settings,defs))
    )),
    `VSpacing(0.3),
    `HVCenter(`PushButton(`id(`details),"&Details..."))
    )
  ));

  any curr = UI(`QueryWidget(`id(`rb),`CurrentButton));
  if( curr == `custom) {
    UI(`ChangeWidget(`id(`details),`Enabled,false));
    UI(`ReplaceWidget(`id(`rp),`PushButton(`id(`next),`opt(`default),"Next")));
  }

  any ret = nil;
  any subret = nil;
  map old_WIDGETS = WIDGETS;

  while(true) {
    ret = processInput();
    y2debug("rettt: %1",ret);

    if(ret == `ok)			/* Save the settings */
      break;
    else if(ret == `abort || ret == `back || ret == `cancel || ret == nil)
      break;				/* Don't save the settings */
    else if(ret == `details || ret ==`next)  {
      y2debug("Running wizzard: %1",ret);
      if(ret!=`custom) {
        string current_defs = UI(`QueryWidget(`id(`rb),`CurrentButton));
        map current_sets = FindSettings(settings,current_defs);
        WIDGETS = defs2options(WIDGETS,current_sets);
      }
	subret = SecurityWizzard();
	y2debug("subret=%1",subret);
	if(subret == `back) continue; /* FIXME: update the current RB? */
	if(subret == `abort || subret == `cancel || subret == nil)
	  ret = nil;
	else
	  ret = `ok;
	break;
    }
    else if(ret == `custom) {		/* Change the [Finish] to [Next] */
      y2debug("custom radio button");
      UI(`ChangeWidget(`id(`details),`Enabled,false));
      UI(`ReplaceWidget(`id(`rp),`PushButton(`id(`next),`opt(`default),"Next")));
    }
    else {				/* Other radio button */
      y2error("SecurityMainDialog: Huh? %1",ret);
      UI(`ChangeWidget(`id(`details),`Enabled,true));
      UI(`ReplaceWidget(`id(`rp),`PushButton(`id(`finish),`opt(`default),"Finish")));
    }
    y2debug("subret: %1",subret);
  }

  UI(`CloseDialog());

  /* [Finish]ed */
  if(ret == `ok && old_WIDGETS!=WIDGETS) {
    string current_defs = UI(`QueryWidget(`id(`rb),`CurrentButton));
    y2debug("current_defs: %1",current_defs);
    map current_sets = FindSettings(settings,current_defs);
    WIDGETS = defs2options(WIDGETS,current_sets);
    return `ok;
  }

  return nil;
};

/* Wizzard for all security settings */

define SecurityWizzard() ``{
  integer current = 0;

  any ret = nil;

  while(true) {
    y2debug("current: %1",current);
    if(current==0) ret = SecurityPasswdDialog();
    else if(current==1) ret = SecurityBootDialog();
    else if(current==2) ret = SecurityLoginDialog();
    else if(current==3) ret = SecurityAdduserDialog();
    else if(current==4) ret = SecurityMiscDialog();

    y2debug("ret: %1",ret);
    if(ret==`next) {
      current=current+1;
      if(current>4) break;
    }
    else if(ret==`back) {
      current=current-1;
      if(current<0) break;
    }
    else if(ret == `abort || ret==nil || ret==`ok) break;
  }

  y2debug("aborting: %1",ret);
  return ret;
};

/* Main security settings dialog */

define SecurityDetailsDialog() ``{
  SuSEDialog("Main", [`back,`finish], `VBox(
    VSeparator(), SecurityItem(`passwd,"Password settings"),
    VSeparator(), SecurityItem(`boot, "Boot settings"),
    VSeparator(), SecurityItem(`login,"Login settings"),
    VSeparator(), SecurityItem(`uid,"User and group ID settings"),
    VSeparator(), SecurityItem(`misc,"Miscellaneous settings"),
    VSeparator(),`VSpacing(2)
  ));

  any ret = nil;
  any subret = nil;
  boolean changed = false;

  while(true) {
    ret = processInput();
    if(ret == `ok)
      break;
    else if(ret == `abort || ret == `back || ret == `cancel || ret == nil)
      break;
    else if(ret == `boot)  subret = SecurityBootDialog();
    else if(ret == `login) subret = SecurityLoginDialog();
    else if(ret == `passwd) subret = SecurityPasswdDialog();
    else if(ret == `misc) subret = SecurityMiscDialog();
    else if(ret == `uid) subret = SecurityAdduserDialog();
    else y2error("SecurityDetailsDialog: Huh? %1",ret);
    if(subret == `ok) changed = true;
  }

  UI(`CloseDialog());

  /* return `ok only if something changed */
  if(ret == `ok && changed == true)
    return `ok;

  return nil;
};

/* Boot settings dialog */

define SecurityBootDialog() ``{
  SuSEDialog("Boot", [], `VBox(
`Frame("Boot permissions",`HBox(`HSpacing(1),`VBox(`VSpacing(-0.1),
    VSeparator(),
    map2widget("CONSOLE_SHUTDOWN"),
    VSeparator(),
    map2widget("KDM_SHUTDOWN"),
    VSeparator()
,`VSpacing(0.3)),`HSpacing(1.5)))
  ));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("CONSOLE_SHUTDOWN");
    updateWidget("KDM_SHUTDOWN");
  }
  UI(`CloseDialog());
  return ret;
};

/* Login and password settings dialog */

define SecurityLoginDialog() ``{
  SuSEDialog("Login", [], `VBox(
//`HVCenter(`HVSquash(`HBox(`HSpacing(5),`VBox(`VSpacing(2),T,`VSpacing(2)),`HSpacing(5)))),
`Frame("Login",`HBox(`HSpacing(1),`VBox(`VSpacing(0.3),
    VSeparator(),
    map2widget("FAIL_DELAY"),
    VSeparator(),
    map2widget("FAILLOG_ENAB"),
    VSeparator(),
    map2widget("LASTLOG_ENAB"),
    VSeparator(),
`VSpacing(0.3)),`HSpacing(1)))
));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("FAILLOG_ENAB");
    updateWidget("LASTLOG_ENAB");
    updateWidget("FAIL_DELAY");
  }
  UI(`CloseDialog());
  return ret;
};

/* Files permissions dialog */

define SecurityPermsDialog() ``{
  SuSEDialog("Perms", [], `VBox(
    VSeparator(),
    map2widget("PERMISSION_SECURITY"),
    VSeparator()
  ));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("PERMISSION_SECURITY");
  }
  UI(`CloseDialog());
  return ret;
};

/* Other security settings dialog */

define SecurityMiscDialog() ``{
  SuSEDialog("Misc", [`back,`abort,`finish], `VBox(
    VSeparator(),
//`Frame("Permissions",`HBox(`HSpacing(1),`VBox(`VSpacing(0.3),
    map2widget("PERMISSION_SECURITY"), /* See the SecurityPermsDialog */
//`VSpacing(0.3)),`HSpacing(1))),
//`VSpacing(1.5),
//`Frame("Other settings",`HBox(`HSpacing(1),`VBox(`VSpacing(0.3),
    VSeparator(),
    map2widget("RUN_UPDATEDB_AS"),
    VSeparator(),
    map2widget("CWD_IN_ROOT_PATH"),
    VSeparator(),
    map2widget("ROOT_LOGIN_REMOTE"),
    VSeparator()
//),`HSpacing(1)))
  ));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("ROOT_LOGIN_REMOTE");
    updateWidget("PERMISSION_SECURITY");
    updateWidget("CWD_IN_ROOT_PATH");
    updateWidget("RUN_UPDATEDB_AS");
  }
  UI(`CloseDialog());
  return ret;
};

/* Password security settings dialog */

define SecurityPasswdDialog() ``{
  SuSEDialog("Password", [], `VBox(
    VSeparator(),
    `Frame("Checks",`VBox(
    map2widget("PASSWD_USE_CRACKLIB"),
    VSeparator(),
    map2widget("OBSCURE_CHECKS_ENAB"),
    VSeparator()
    )),
    `Frame("Password length",`HBox(
    map2widget("PASS_MIN_LEN"),
    VSeparator(),
    map2widget("PASS_MAX_LEN"),
    VSeparator()
    )),
    `Frame("Days of password change warning",`HBox(
    map2widget("PASS_MIN_DAYS"),
    VSeparator(),
    map2widget("PASS_MAX_DAYS"),
    VSeparator()
    )),
    VSeparator(),
    map2widget("PASS_WARN_AGE"),
    VSeparator()
    //,`HSpacing(1)
  ));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("PASS_MIN_DAYS");
    updateWidget("PASS_MAX_DAYS");
    updateWidget("PASS_MIN_LEN");
    updateWidget("PASS_MAX_LEN");
    updateWidget("PASSWD_USE_CRACKLIB");
    updateWidget("OBSCURE_CHECKS_ENAB");
    updateWidget("PASS_WARN_AGE");
  }
  UI(`CloseDialog());
  return ret;
};

/* Adduser security settings dialog */

define SecurityAdduserDialog() ``{
  SuSEDialog("Adduser", [], `VBox(
    VSeparator(),
    `Frame("User ID limitations",`HBox(
    map2widget("UID_MIN"),
    VSeparator(),
    map2widget("UID_MAX")
    )),
    VSeparator(),
    `Frame("Group ID limitations",`HBox(
    map2widget("GID_MIN"),
    VSeparator(),
    map2widget("GID_MAX")
    )),
    VSeparator()
    //,`HSpacing(1)
  ));
  any ret = processInput();
  if(ret == `ok) {
    updateWidget("GID_MIN");
    updateWidget("GID_MAX");
    updateWidget("UID_MIN");
    updateWidget("UID_MAX");
  }
  UI(`CloseDialog());
  return ret;
};

} /* YaST2: Local security configuration EOF */

