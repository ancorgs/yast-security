/**
 * File:	include/security/wizards.ycp
 * Package:	Security configuration
 * Summary:	Wizards definitions
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "security";

import "Wizard";

include "wizard/sequencer.ycp";
include "security/dialogs.ycp";

/**
 * Whole configuration of security
 * @return any Returned value from WizardSequencer() call
 */
global define any SecuritySequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("Security configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    any ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of security but without reading and writing.
 * For use with autoinstallation.
 * @return any Returned value from WizardSequencer() call
 */
global define any SecurityAutoSequence() ``{
    string caption = _("Security configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    /* Run the main configuration workflow */
    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the security configuration
 * @return any Returned value from WizardSequencer() call
 */
global define any MainSequence() ``{

    map aliases = $[
	"main"     : ``(MainDialog()),
	"password" : ``(PassDialog()),
	"boot"     : ``(BootDialog()),
	"login"    : ``(LoginDialog()),
	"adduser"  : ``(UserDialog()),
	"misc"     : ``(MiscDialog()),
	"finish"   : [ ``(Finish()), true ]
    ];

    map sequence = $[
	"ws_start" : "main",
	"main" : $[
	    `abort : `abort,
	    `next : "password",
	    `finish : "finish"
	],
	"password" : $[
	    `abort : `abort,
	    `next : "boot"
	],
	"boot" : $[
	    `abort : `abort,
	    `next : "login"
	],
	"login" : $[
	    `abort : `abort,
	    `next : "adduser"
	],
	"adduser" : $[
	    `abort : `abort,
	    `next : "misc"
	],
	"misc" : $[
	    `abort : `abort,
	    `finish : "finish"
	],
	"finish" : $[
	    `finish : `next
	]
    ];

    any ret = WizardSequencer(aliases, sequence);

    return ret;
}

/* EOF */
}
