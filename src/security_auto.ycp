/**
 * File:	clients/security_auto.ycp
 * Package:	Security configuration
 * Summary:	Client for autoinstallation
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of security settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("security_auto", [ "Summary", mm ]);
 */

{

textdomain "security";

y2milestone("----------------------------------------");
y2milestone("Security auto started");

import "Security";
include "security/wizards.ycp";

any ret = nil;
string func = "";
map param = $[];


/**
 * Switch map keys to lower case
 * @param map with upper case
 * @return map with lower case
 */
define  switch2Lower(map s) ``{
	map new_s = mapmap(`k,`v, s, ``{
			return([tolower(k),v]);
			});
	return new_s;
}

/**
 * Switch map keys to upper case
 * @param map with low case
 * @return map with upper case
 */
define  switch2Upper(map s) ``{
	map new_s = mapmap(`k,`v, s, ``{
			return([toupper(k),v]);
			});
	return new_s;
}


/* Check arguments */
if(size(Args()) > 0 && is(Args(0), string)) {
    func = WFM::Args(0);
    if(size(Args()) > 1 && is(Args(1), map))
	param = WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Import Data*/
if(func == "Import") {
    ret = Security::Import(switch2Upper(param));
}
/* Create a  summary*/
else if(func == "Summary") {
    ret = select(Security::Summary(), 0, "");
}
/* Reset configuration */
else if (func == "Reset") {
    Security::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    ret = SecurityAutoSequence();
}
/* Return actual state */
else if (func == "Result") {
    ret = switch2Lower(Security::Export());
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    Security::write_only = true;
    Progress::off();
    ret = Security::Write();
    Progress::on();
}
/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Security auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
